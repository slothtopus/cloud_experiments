#version 300 es

precision mediump float;
precision lowp sampler3D;

  #define MAX_STEPS 100
  #define MAX_DIST 100.
  #define SURF_DIST .01

in vec2 v_position;

uniform mat4 u_box_transform_inverse;
uniform mat4 u_box_transform;
uniform mat4 u_light_transform;
uniform float u_stepsize;
uniform float u_time;
uniform int u_max_depth_steps;
uniform vec2 u_resolution;

uniform sampler3D uTexture;

out vec4 fragColor; // Declare output fragment color

vec3 randomGradient3d(vec3 p, float seed) {
    p = p + 0.01f;
    float x = dot(p, vec3(123.4f, 234.5f, 345.6f));
    float y = dot(p, vec3(234.5f, 345.6f, 456.7f));
    float z = dot(p, vec3(345.6f, 456.7f, 567.8f));
    vec3 gradient = vec3(x, y, z);
    gradient = sin(gradient);
    gradient *= (43758.5453f + seed);
    //gradient = sin(gradient + u_time * 8.f);
    gradient = sin(gradient);
    return gradient;
}

float perlin3d(vec3 uv, float k, float seed) {
    uv = uv * k;

    vec3 gridId = floor(uv);
    vec3 gridUv = fract(uv);

    // using right-handed coordinate system
    // f = front, r = rear
    vec3 rbl = gridId + vec3(0, 0, 0);
    vec3 rbr = gridId + vec3(1, 0, 0);
    vec3 rtl = gridId + vec3(0, 1, 0);
    vec3 rtr = gridId + vec3(1, 1, 0);

    vec3 fbl = gridId + vec3(0, 0, 1);
    vec3 fbr = gridId + vec3(1, 0, 1);
    vec3 ftl = gridId + vec3(0, 1, 1);
    vec3 ftr = gridId + vec3(1, 1, 1);

    vec3 gradRbl = randomGradient3d(rbl, seed);
    vec3 gradRbr = randomGradient3d(rbr, seed);
    vec3 gradRtl = randomGradient3d(rtl, seed);
    vec3 gradRtr = randomGradient3d(rtr, seed);

    vec3 gradFbl = randomGradient3d(fbl, seed);
    vec3 gradFbr = randomGradient3d(fbr, seed);
    vec3 gradFtl = randomGradient3d(ftl, seed);
    vec3 gradFtr = randomGradient3d(ftr, seed);

    vec3 vFromRbl = gridUv - vec3(0, 0, 0);
    vec3 vFromRbr = gridUv - vec3(1, 0, 0);
    vec3 vFromRtl = gridUv - vec3(0, 1, 0);
    vec3 vFromRtr = gridUv - vec3(1, 1, 0);

    vec3 vFromFbl = gridUv - vec3(0, 0, 1);
    vec3 vFromFbr = gridUv - vec3(1, 0, 1);
    vec3 vFromFtl = gridUv - vec3(0, 1, 1);
    vec3 vFromFtr = gridUv - vec3(1, 1, 1);

    float dotRbl = dot(gradRbl, vFromRbl);
    float dotRbr = dot(gradRbr, vFromRbr);
    float dotRtl = dot(gradRtl, vFromRtl);
    float dotRtr = dot(gradRtr, vFromRtr);

    float dotFbl = dot(gradFbl, vFromFbl);
    float dotFbr = dot(gradFbr, vFromFbr);
    float dotFtl = dot(gradFtl, vFromFtl);
    float dotFtr = dot(gradFtr, vFromFtr);

    gridUv = smoothstep(0.0f, 1.0f, gridUv);

    float Rb = mix(dotRbl, dotRbr, gridUv.x);
    float Rt = mix(dotRtl, dotRtr, gridUv.x);
    float R = mix(Rb, Rt, gridUv.y);

    float Fb = mix(dotFbl, dotFbr, gridUv.x);
    float Ft = mix(dotFtl, dotFtr, gridUv.x);
    float F = mix(Fb, Ft, gridUv.y);

    float perlin = mix(R, F, gridUv.z);

    return perlin;
}

float getNoise(vec3 p) {
    return texture(uTexture, p).r;
    //return perlin3d(p, 10.0f, 123.4f) + 0.75f;

}

float sdBox(vec3 p, vec3 b) {
    vec3 q = abs(p) - b;
    return length(max(q, 0.0f)) + min(max(q.x, max(q.y, q.z)), 0.0f);
}

float sdEllipsoid(vec3 p, vec3 r) {
    float k0 = length(p / r);
    float k1 = length(p / (r * r));
    return k0 * (k0 - 1.0f) / k1;
}

/*float sdBoxDistorted(vec3 p, vec3 b) {
    vec3 q = abs(p) - b;
    float d = length(max(q, 0.0f)) + min(max(q.x, max(q.y, q.z)), 0.0f);

    //float textureValue = getNoise(p / 10.0f);
    return d - textureValue * 0.95f;
}*/

float carveUp(vec3 p, float d) {
    # if 0
    float textureValue = getNoise(p / 10.0f + vec3(sin(u_time / 3.0f), -u_time / 6.0f, u_time / 3.0f));
    # else
    float textureValue = getNoise(p / 15.0f);
    # endif
    return d - textureValue * 1.5f;
}

float sceneDistanceTransformed(vec3 p) {
    vec4 p_transformed = u_box_transform_inverse * vec4(p, 1);
    return carveUp(p_transformed.xyz, sdBox(p_transformed.xyz, vec3(4, 2, 2)));
    return carveUp(p_transformed.xyz, sdEllipsoid(p_transformed.xyz, vec3(4, 2, 2)));
}

vec3 GetNormal(vec3 p) {
    float d = sceneDistanceTransformed(p);
    vec2 e = vec2(.01f, 0);

    vec3 n = d - vec3(sceneDistanceTransformed(p - e.xyy), sceneDistanceTransformed(p - e.yxy), sceneDistanceTransformed(p - e.yyx));

    return normalize(n);
}

float GetLight(vec3 p, vec3 lightPos) {
    vec3 l = normalize(lightPos - p);
    vec3 n = GetNormal(p);

    float dif = clamp(dot(n, l), 0.f, 1.f);
    return dif;
}

float RayMarch(vec3 ro, vec3 rd) {
    float dO = 0.f;
    for(int i = 0; i < MAX_STEPS; i++) {
        vec3 p = ro + rd * dO;
        float dS = sceneDistanceTransformed(p);
        dO += dS;
        if(dO > MAX_DIST || dS < SURF_DIST)
            break;
    }
    return dO;
}

void main() {
    vec3 ro = vec3(0, 0, 5);
    float zoom = 3.0f;
    vec3 rd = normalize(vec3(v_position.x * (u_resolution.x / u_resolution.y) / zoom, v_position.y / zoom, -1));

    float d = RayMarch(ro, rd);

    fragColor = vec4(0, 0, 0, 1);
    if(d <= 20.0f) {
        float l = GetLight(ro + rd * d, vec3(0, 10, 0));
        fragColor = vec4(l, l, l, 1.f);
    }
}